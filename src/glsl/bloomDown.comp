#version 460
#pragma shader_stage(compute)

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform sampler2D source;
layout(binding = 1, r11f_g11f_b10f) restrict writeonly uniform image2D target;

shared vec4 outer[gl_WorkGroupSize.x + 2][gl_WorkGroupSize.y + 2];
shared vec4 inner[gl_WorkGroupSize.x + 1][gl_WorkGroupSize.y + 1];

void main() {
	
	const uvec2 gid = gl_GlobalInvocationID.xy;
	const uvec2 lid = gl_LocalInvocationID.xy;
	const ivec2 sourceSize = textureSize(source, 0);
	const ivec2 targetSize = imageSize(target);
	
	vec2 uv = (vec2(gid) + vec2(0.5)) / vec2(targetSize);
	vec2 texel = vec2(1.0) / vec2(sourceSize);
	vec2 texel2 = texel * 2.0;
	
	if (lid.x <= 1)
		outer[lid.x  ][lid.y+2] = textureLod(source, uv + vec2(-texel2.x, texel2.y), 0.0);
	if (lid.y <= 1)
		outer[lid.x+2][lid.y  ] = textureLod(source, uv + vec2(texel2.x, -texel2.y), 0.0);
	if (lid.x <= 1 && lid.y <= 1)
		outer[lid.x  ][lid.y  ] = textureLod(source, uv - texel2, 0.0);
	
	outer[lid.x+2][lid.y+2] = textureLod(source, uv + texel2, 0.0);
	
	if (lid.x == 0)
		inner[lid.x  ][lid.y+1] = textureLod(source, uv + vec2(-texel.x, texel.y), 0.0);
	if (lid.y == 0)
		inner[lid.x+1][lid.y  ] = textureLod(source, uv + vec2(texel.x, -texel.y), 0.0);
	if (lid.x == 0 && lid.y == 0)
		inner[lid.x  ][lid.y  ] = textureLod(source, uv - texel, 0.0);
	
	inner[lid.x+1][lid.y+1] = textureLod(source, uv + texel, 0.0);
	
	memoryBarrierShared();
	barrier();
	
	if (any(greaterThanEqual(gid, targetSize)))
		return;
	
	vec4 resultInner =
		inner[lid.x  ][lid.y  ] * (4.0 / 32.0) +
		inner[lid.x+1][lid.y  ] * (4.0 / 32.0) +
		inner[lid.x  ][lid.y+1] * (4.0 / 32.0) +
		inner[lid.x+1][lid.y+1] * (4.0 / 32.0);
	vec4 resultOuter =
		outer[lid.x  ][lid.y  ] * (1.0 / 32.0) +
		outer[lid.x+1][lid.y  ] * (2.0 / 32.0) +
		outer[lid.x+2][lid.y  ] * (1.0 / 32.0) +
		outer[lid.x  ][lid.y+1] * (2.0 / 32.0) +
		outer[lid.x+1][lid.y+1] * (4.0 / 32.0) +
		outer[lid.x+2][lid.y+1] * (2.0 / 32.0) +
		outer[lid.x  ][lid.y+2] * (1.0 / 32.0) +
		outer[lid.x+1][lid.y+2] * (2.0 / 32.0) +
		outer[lid.x+2][lid.y+2] * (1.0 / 32.0);
	imageStore(target, ivec2(gid), resultInner + resultOuter);
	
}
